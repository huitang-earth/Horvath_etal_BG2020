#! /bin/csh -f

#################################################################################
if ($PHASE == set_batch) then
#################################################################################

source ./Tools/ccsm_getenv || exit -1

set ntasks  = `${CASEROOT}/Tools/taskmaker.pl -sumonly`
set maxthrds = `${CASEROOT}/Tools/taskmaker.pl -maxthrds`
@ nodes = $ntasks / ${MAX_TASKS_PER_NODE}
if ( $ntasks % ${MAX_TASKS_PER_NODE} > 0) then
  @ nodes = $nodes + 1
  @ ntasks = $nodes * ${MAX_TASKS_PER_NODE}
endif
@ taskpernode = ${MAX_TASKS_PER_NODE} / ${maxthrds}
set qname = batch
set tlimit = "00:59:00"
if ($CCSM_ESTCOST > 2) set tlimit = "08:59:00"

if ($?TESTMODE) then
 set file = $CASEROOT/${CASE}.test 
else
 set file = $CASEROOT/${CASE}.run 
endif

cat >! $file << EOF1
#!/bin/csh -f
#SBATCH --job-name=${CASE}
#SBATCH --time=$tlimit
#SBATCH --ntasks=$ntasks
#SBATCH --account=nn1000k
#SBATCH --mem-per-cpu=4G
#SBATCH --cpus-per-task=$maxthrds
#SBATCH --output=slurm.out
###SBATCH --mail-type=END
###SBATCH --mail-user=xyz@abc.com

EOF1

#################################################################################
else if ($PHASE == set_exe) then
#################################################################################

set maxthrds = `${CASEROOT}/Tools/taskmaker.pl -maxthrds`
set maxtasks = `${CASEROOT}/Tools/taskmaker.pl -sumtasks`

cat >> ${CASEROOT}/${CASE}.run << EOF1
sleep 25
cd \$RUNDIR
echo "\`date\` -- CSM EXECUTION BEGINS HERE" 

setenv OMP_NUM_THREADS ${maxthrds}
# for mvapich
#srun --ntasks=${maxtasks} --cpu_bind=sockets --cpu_bind=verbose --kill-on-bad-exit \$EXEROOT/cesm.exe >&! cesm.log.\$LID
# for mvapich2
#srun --mpi=none --ntasks=${maxtasks} --cpu_bind=sockets --cpu_bind=verbose --kill-on-bad-exit \$EXEROOT/cesm.exe >&! cesm.log.\$LID
mpirun  \$EXEROOT/cesm.exe >&! cesm.log.\$LID

#setenv MV2_ENABLE_AFFINITY 1
#setenv MV2_CPU_MAPPING 31:29:27:25:23:21:19:17:15:13:11:9:7:5:3:1
#srun --mpi=none -n=${maxtasks} --kill-on-bad-exit \$EXEROOT/cesm.exe >&! cesm.log.\$LID

wait
echo "\`date\` -- CSM EXECUTION HAS FINISHED" 

EOF1

#################################################################################
else if ($PHASE == set_larch) then
#################################################################################

   #This is a place holder for a long-term archiving script

cat >! $CASEROOT/${CASE}.l_archive << EOF1
#!/bin/csh -f
#SBATCH --job-name=${CASE}
#SBATCH --time=04:00:00
#SBATCH --ntasks=1
#SBATCH --account=nn1000k
#SBATCH --mem-per-cpu=4G
#SBATCH --output=archive.out
###SBATCH --mail-type=END
###SBATCH --mail-user=xyz@abc.com

cd $CASEROOT 
source ./Tools/ccsm_getenv || exit -1
setenv LID "\`date +%y%m%d-%H%M%S\`"

# copy output files to norSTORE -- the UIO medium-term storage system

cd \$DOUT_S_ROOT


$CASEROOT/Tools/lt_archive.sh --mode copy_dirs_sshscp --ssh_loc hexagon.bccs.uib.no --scp_loc login3.norstore.uio.no

wait

exit 0

EOF1
chmod 775 ${CASEROOT}/${CASE}.l_archive


#################################################################################
else
#################################################################################

    echo "  PHASE setting of $PHASE is not an accepted value"
    echo "  accepted values are set_batch, set_exe and set_larch"
    exit 1

#################################################################################
endif
#################################################################################
